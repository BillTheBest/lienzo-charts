/*
 Copyright (c) 2014,2015 Ahome' Innovation Technologies. All rights reserved.
 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at
 http://www.apache.org/licenses/LICENSE-2.0
 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
 */
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

version = '1.0.125-RC1'

group = 'com.ahome-it'

task doJavaDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: doJavaDocs) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

compileJava.dependsOn(clean)

jar {
    manifest {
        attributes 'Implementation-Title': 'Lienzo-Charts', 'Implementation-Version': version
    }
    from sourceSets.main.allSource
}

repositories {
    mavenCentral()
    maven { url "${sonatype_snapshotURL}" }
}

sourceSets {
    main {
        resources {
            source java
        }
    }
}

dependencies {
    compile(group: 'com.google.gwt', name: 'gwt-user', version: '2.6.1')
    compile(group: 'com.ahome-it', name: 'lienzo-core', version: '2.0.121-RC1')
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: "${sonatype_user}", password: "${sonatype_password}")
            }
            pom.project {
                name 'Lienzo-Charts'
                packaging 'jar'
                description 'Lienzo-Charts is GWT library for displaying charts built on the Lienzo Structure Graphics Toolkit'
                url 'https://github.com/ahome-it/lienzo-charts'
                scm {
                    url 'scm:git@github.com:ahome-it/lienzo-charts.git'
                    connection 'scm:git@github.com:ahome-it/lienzo-charts.git'
                    developerConnection 'scm:git@github.com:ahome-it/lienzo-charts.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'SprocketNYC'
                        name 'Dean Jones'
                    }
                }
            }
        }
    }
}